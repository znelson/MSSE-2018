
MODULE main
	VAR
		-- input variables
		
		-- input variables representing pedestrian walk requests in two directions
		inNSWalkRequest : boolean;
		inWEWalkRequest : boolean;

		-- input variables representing emergency vehicle presence in two directions
		inNSEmergency : boolean;
		inWEEmergency : boolean;

		-- input variables representing cars waiting in two directions
		inNSCarsWaiting : boolean;
		inWECarsWaiting : boolean;

		-- output variables

		-- output variables representing the color of the traffic lights in two directions
		outNSCarLight : {Green, Yellow, Red};
		outWECarLight : {Green, Yellow, Red};

		-- output variables representing the state of the pedestrian walk lights in two directions
		outNSWalkLight : {Walk, Stop};
		outWEWalkLight : {Walk, Stop};

		-- state variables

		trafficMode : {NSGo, NSSlow, NSStop, WEGo, WESlow, WEStop, ResetTrafficMode};

	DEFINE
		emergency := inNSEmergency | inWEEmergency;
		
		redEverywhere := outNSCarLight = Red & outWECarLight = Red;
		
		yellowAnywhere := trafficMode in {NSSlow, WESlow};
		
		greenAnywhere := trafficMode in {NSGo, WEGo};

		waitingWE := inWEWalkRequest | inWECarsWaiting;

		waitingNS := inNSWalkRequest | inNSCarsWaiting;

	ASSIGN


		init(trafficMode) := ResetTrafficMode;
		next(trafficMode) :=
			case
				trafficMode=ResetTrafficMode & !emergency		: NSGo;

				trafficMode=NSGo & (waitingWE | emergency)		: NSSlow;

				trafficMode=NSSlow								: NSStop;

				trafficMode=NSStop & !emergency : WEGo;
				trafficMode=WEGo & (waitingNS | emergency)		: WESlow;

				trafficMode=WESlow								: WEStop;

				trafficMode=WEStop & !emergency					: NSGo;

				TRUE											: trafficMode;
			esac;

		init(outNSCarLight) := Red;
		next(outNSCarLight) := 
			case
				next(trafficMode)=NSGo							: Green;
				next(trafficMode)=NSSlow						: Yellow;
				TRUE											: Red;
			esac;

		init(outWECarLight) := Red;
		next(outWECarLight) := 
			case
				next(trafficMode)=WEGo							: Green;
				next(trafficMode)=WESlow						: Yellow;
				TRUE											: Red;
			esac;

		init(outNSWalkLight) := Stop;
		next(outNSWalkLight) := 
			case
				next(trafficMode)=NSGo							: Walk;
				TRUE											: Stop;
			esac;

		init(outWEWalkLight) := Stop;
		next(outWEWalkLight) := 
			case
				next(trafficMode)=WEGo							: Walk;
				TRUE											: Stop;
			esac;

	
	-- Justice statement to prevent infinite emergencies either direction.
	JUSTICE !emergency;

	-- Justice statement to prevent infinite cars or pedestrians waiting in either direction.
	JUSTICE !waitingWE;
	JUSTICE waitingWE;
	JUSTICE !waitingNS;
	JUSTICE waitingNS;


	-- Liveness properties:
	-----------------------

	-- If cars are waiting, they will eventually get to go (or stop waiting)
	CTLSPEC AG(inNSCarsWaiting -> AF(outNSCarLight = Green | !inNSCarsWaiting))
	CTLSPEC AG(inWECarsWaiting -> AF(outWECarLight = Green | !inWECarsWaiting))

	-- If pedestrians are waiting, they will eventually get to walk (or stop waiting)
	CTLSPEC AG(inNSWalkRequest -> AF(outNSWalkLight = Walk | !inNSWalkRequest))
	CTLSPEC AG(inNSWalkRequest -> AF(outWEWalkLight = Walk | !inNSWalkRequest))

	
	-- Safety properties:
	---------------------

	-- One of the two directions always show a Red light
	CTLSPEC AG(outNSCarLight = Red | outWECarLight = Red)
	CTLSPEC AG(outNSWalkLight = Stop | outWEWalkLight = Stop)

	-- When pedestrians have a Walk light, the opposite direction car light is Red
	CTLSPEC AG(outNSWalkLight = Walk -> outWECarLight = Red)
	CTLSPEC AG(outWEWalkLight = Walk -> outNSCarLight = Red)

	-- Immediately after a Yellow light, all lights turn Red
	CTLSPEC AG(outNSCarLight = Yellow -> AX redEverywhere)
	CTLSPEC AG(outWECarLight = Yellow -> AX redEverywhere)

	
	-- Emergency vehicles:
	----------------------

	-- If an emergency vehicle arrives and a light is Green, all lights will turn Red in exactly 2 steps
	CTLSPEC AG((emergency & greenAnywhere) -> AX(AX(redEverywhere)))
	-- If an emergency vehicle arrives and a light is Yellow, all lights will turn Red in exactly 1 step
	CTLSPEC AG((emergency & yellowAnywhere) -> AX(redEverywhere))
	-- If an emergency vehicle arrives and all lights are Red, all lights will remain Red in the next step
	CTLSPEC AG((emergency & redEverywhere) -> AX(redEverywhere))

	-- If there are no emergency vehicles, and no cars or pedestrians waiting, lights will remain Green.
	CTLSPEC AG(!emergency & outNSCarLight = Green & !waitingWE -> AX(outNSCarLight = Green))
	CTLSPEC AG(!emergency & outWECarLight = Green & !waitingNS -> AX(outWECarLight = Green))

	-- Each light will go to Yellow between Green and Red
	LTLSPEC G(Y(outNSCarLight = Green) & X(outNSCarLight = Red) -> outNSCarLight = Yellow)
	LTLSPEC G(Y(outWECarLight = Green) & X(outWECarLight = Red) -> outWECarLight = Yellow)


	-- Anti-properties:
	-------------------

	-- The lights can fail to cycle (eventually) from Green North-South -> Green West-East -> Green North-South
	-- ("The lights are stuck!")
	CTLSPEC !EF(outWECarLight = Green & EF(outNSCarLight = Green & EF(outWECarLight = Green)))

	-- Walk lights can be on in both directions
	-- ("This intersection eventually lets everybody walk at once!")
	CTLSPEC EF(outWEWalkLight = Walk & outNSWalkLight = Walk)

